plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'origin/main'
            standardOutput = code
        }
        return Integer.parseInt(code.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return '0.0.0';
    }
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "top.rootu.lampa"
        minSdkVersion 16
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        multiDexEnabled = true
        // Use SupportLibrary for vectors
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        // https://stackoverflow.com/a/55121645
        useLibrary 'org.apache.http.legacy'
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropsFile = file("keystore/keystore_config")

            if (keystorePropsFile.exists()) {
                file("keystore/keystore_config").withInputStream { keystoreProperties.load(it) }
                storeFile file("$keystoreProperties.storeFile")
                storePassword "$keystoreProperties.storePassword"
                keyAlias "$keystoreProperties.keyAlias"
                keyPassword "$keystoreProperties.keyPassword"
            } else {
                storeFile file(System.getenv('KEYSTORE_FILE'))
                storePassword System.getenv('KEYSTORE_PASSWORD')
                keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
                keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // gradle 4.6 migration: disable dimensions mechanism
    // more: https://proandroiddev.com/advanced-android-flavors-part-4-a-new-version-fc2ad80c01bb
    flavorDimensions "default"

    productFlavors {
        lite {}
        full {}
    }
    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // multidex
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.activity:activity-ktx:1.6.0-alpha03'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
//    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
//    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
//    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.0-alpha03'
//    implementation 'androidx.navigation:navigation-ui-ktx:2.5.0-alpha03'
    implementation 'com.google.android.material:material:1.5.0'
    //speech
    implementation 'net.gotev:speech:1.6.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // https://download.01.org/crosswalk/releases/crosswalk/android/stable/latest/
    liteApi 'org.xwalk:xwalk_shared_library:23.53.589.4'
    fullApi 'org.xwalk:xwalk_core_library:23.53.589.4'
}